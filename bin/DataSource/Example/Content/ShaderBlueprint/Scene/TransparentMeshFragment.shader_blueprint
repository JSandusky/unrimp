/*********************************************************\
 * Copyright (c) 2012-2018 The Unrimp Team
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy of this software
 * and associated documentation files (the "Software"), to deal in the Software without
 * restriction, including without limitation the rights to use, copy, modify, merge, publish,
 * distribute, sublicense, and/or sell copies of the Software, and to permit persons to whom the
 * Software is furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all copies or
 * substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING
 * BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
 * NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM,
 * DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING
 * FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
\*********************************************************/


//[-------------------------------------------------------]
//[ Definitions                                           ]
//[-------------------------------------------------------]
@includepiece($ProjectName/ShaderPiece/Core.asset)
	@insertpiece(SetCrossPlatformSettings)


//[-------------------------------------------------------]
//[ Define pieces                                         ]
//[-------------------------------------------------------]
// TODO(co) Define this outside
@piece(MaximumNumberOfMaterials)2@end


//[-------------------------------------------------------]
//[ Input / output                                        ]
//[-------------------------------------------------------]
// Attribute input / output
INPUT_BEGIN
	INPUT_TEXTURE_COORDINATE_NOINTERPOLATION(1, uint,   AssignedMaterialSlotVS,			0)	// The assigned material slot inside the material uniform buffer
	INPUT_TEXTURE_COORDINATE				(2, float2, TexCoordVS,						1)	// Texture coordinate
	INPUT_TEXTURE_COORDINATE				(3, float3, TangentFrame0VS,				2)	// Tangent frame
	INPUT_TEXTURE_COORDINATE				(4, float3, TangentFrame1VS,				3)	// Tangent frame
	INPUT_TEXTURE_COORDINATE				(5, float3, TangentFrame2VS,				4)	// Tangent frame
	INPUT_TEXTURE_COORDINATE				(6, float3, WorldSpacePositionVS,			5)	// World space position
	INPUT_TEXTURE_COORDINATE				(7, float3, PreviousClipSpacePositionVS,	6)	// Previous clip space position
	DECLARE_FRAGMENT_POSITION
INPUT_END
OUTPUT_BEGIN
	OUTPUT_COLOR(0)	// rgb = albedo color, a = alpha
OUTPUT_END

// Uniform buffers
struct PassDataStruct
{
	float4x4 WorldSpaceToClipSpaceMatrix[2];
	float4x4 PreviousWorldSpaceToClipSpaceMatrix;
	float4x4 ShadowMatrix;
	float4   ShadowCascadeOffsets[4];
	float4   ShadowCascadeScales[4];
	float4   ShadowCascadeSplits;
	float4	 ViewSpaceToWorldSpaceQuaternion;
	float4	 WorldSpaceToViewSpaceQuaternion;
	float3	 ViewSpaceSunlightDirection;
	float	 Wetness;
	float3	 AmbientColor;
	float3	 SunlightColor;
	float3   CameraWorldSpacePosition;
	float3	 LightClustersScale;
	float3	 LightClustersBias;
	uint	 FullCoverageMask;
	float2   ViewportSize;
	float2   ProjectionParametersReversedZ;
};
UNIFORM_BUFFER_BEGIN(0, 0, PassUniformBuffer, 0)
	PassDataStruct PassData;
UNIFORM_BUFFER_END
struct Material
{
	float3 AlbedoColor;
	float  Roughness;
	float  Metallic;
	float  AlphaReference;
	float  EmissiveIntensity;
	float  Opacity;
};
UNIFORM_BUFFER_BEGIN(1, 0, MaterialUniformBuffer, 1)
	Material Materials[FAST_SHADER_BUILD_HACK(@insertpiece(MaximumNumberOfMaterials))];
UNIFORM_BUFFER_END

// Texture buffers: We need to start at texture unit 1 instead of texture unit 0 because the vertex shader has an instance texture buffer bound at texture unit 0 (OpenGL shares those bindings accros all shader stages while Direct3D doesn't)
TEXTURE_BUFFER(2, 2, float4, LightTextureBuffer, 1)	// "LIGHT"

// Textures
TEXTURE_2D_ARRAY(2, 3, ShadowMap, 2)
TEXTURE_CUBE(2, 4, ReflectionCubeMap, 3)
TEXTURE_3D_UINT(2, 5, LightClustersMap3D, 4)
TEXTURE_2D(3, 0, _argb_nxa, 5)		// RGB channel = Albedo map ("_a"-postfix), A channel = x component of normal map ("_n"-postfix)
TEXTURE_2D(3, 1, _hr_rg_mb_nya, 6)	// R channel = Height map ("_h"-postfix), G channel = Roughness map ("_r"-postfix), B channel = Metallic map ("_m"-postfix), A channel = y component of normal map ("_n"-postfix)
@property(UseAlphaMap)
	TEXTURE_2D(3, 2, AlphaMap, 7)
@end
@property(UseEmissiveMap)
	TEXTURE_2D(3, 3, EmissiveMap, 8)
@end

// Samplers
SAMPLER_STATE(4, 0, SamplerPoint, 0)
SAMPLER_STATE(4, 1, SamplerLinear, 1)


//[-------------------------------------------------------]
//[ Functions                                             ]
//[-------------------------------------------------------]
@includepiece($ProjectName/ShaderPiece/TangentFrame.asset)
	@insertpiece(DefineGetTangentFrame)
@includepiece($ProjectName/ShaderPiece/PhysicallyBasedShading.asset)
	@property(HighQualityLighting)
		@insertpiece(DefinePhysicallyBasedShading)
	@end
	@property(!HighQualityLighting)
		@insertpiece(DefineBlinnPhongBasedShading)
	@end
@includepiece($ProjectName/ShaderPiece/Depth.asset)
	@insertpiece(DefineGetLinearDepthReversedZ)
@includepiece($ProjectName/ShaderPiece/ExponentialShadow.asset)
	@insertpiece(DefineExponentialShadow)
@includepiece($ProjectName/ShaderPiece/Shadow.asset)
	@insertpiece(DefineCalculateShadowVisibility)


//[-------------------------------------------------------]
//[ Main                                                  ]
//[-------------------------------------------------------]
MAIN_BEGIN
	// Get the used material
	Material material = Materials[MAIN_INPUT(AssignedMaterialSlotVS)];

	// Perform alpha map based fragment rejection
	@property(UseAlphaMap)
		if (material.AlphaReference >= SAMPLE_2D(AlphaMap, SamplerLinear, MAIN_INPUT(TexCoordVS)).r)
		{
			discard;
		}
	@end

	// Read channel packed texture data
	// -> "_argb_nxa" = RGB channel = Albedo map ("_a"-postfix), A channel = x component of normal map ("_n"-postfix)
	// -> "_hr_rg_mb_nya" = R channel = Height map ("_h"-postfix), G channel = Roughness map ("_r"-postfix), B channel = Metallic map ("_m"-postfix), A channel = y component of normal map ("_n"-postfix)
	float4 value_argb_nxa = SAMPLE_2D(_argb_nxa, SamplerLinear, MAIN_INPUT(TexCoordVS));
	float4 value_hr_rg_mb_nya = SAMPLE_2D(_hr_rg_mb_nya, SamplerLinear, MAIN_INPUT(TexCoordVS));

	// Get albedo, roughness and metallic
	float3 albedo = material.AlbedoColor * value_argb_nxa.rgb;
	float roughness = material.Roughness * value_hr_rg_mb_nya.g;
	float metallic = SATURATE(PassData.Wetness + material.Metallic + value_hr_rg_mb_nya.b);

	// Get the per fragment normal [0..1] by using a tangent space BC5/3DC/ATI2N stored normal map
	// -> See "Real-Time Normal Map DXT Compression" -> "3.3 Tangent-Space 3Dc" - http://www.nvidia.com/object/real-time-normal-map-dxt-compression.html
	float3 viewSpaceNormal;
	viewSpaceNormal.x = value_argb_nxa.a * 2.0f - 1.0f;
	viewSpaceNormal.y = value_hr_rg_mb_nya.a * 2.0f - 1.0f;
	viewSpaceNormal.z = sqrt(1.0f - dot(viewSpaceNormal.xy, viewSpaceNormal.xy));
	// TODO(co) Get rid of this
	if (isnan(viewSpaceNormal.z))
	{
		viewSpaceNormal.z = 1.0f;
	}

	// Transform the tangent space normal into view space
	viewSpaceNormal = normalize(viewSpaceNormal.x * MAIN_INPUT(TangentFrame0VS) + viewSpaceNormal.y * MAIN_INPUT(TangentFrame1VS) + viewSpaceNormal.z * MAIN_INPUT(TangentFrame2VS));

	// Derive data
	float3 worldSpaceNormal = MultiplyQuaternionVector(PassData.ViewSpaceToWorldSpaceQuaternion, viewSpaceNormal);
	float3 worldSpacePosition = MAIN_INPUT(WorldSpacePositionVS);
	float3 worldSpaceIncident = normalize(PassData.CameraWorldSpacePosition - worldSpacePosition);
	float3 viewSpaceIncident = MultiplyQuaternionVector(PassData.WorldSpaceToViewSpaceQuaternion, worldSpaceIncident);

	// Get reflection color
	@insertpiece(CalculateReflectionColor)

	// Ambient term
	float3 color = (reflectionColor * metallic + albedo) * (PassData.AmbientColor.rgb + CalculateHemisphereLighting(worldSpaceNormal.xyz, PassData.AmbientColor.rgb * 0.7f, PassData.AmbientColor.rgb * 0.2f));

	// Directional sunlight, our primary light
	float shadowVisibility = CalculateShadowVisibility(worldSpacePosition, GetLinearDepthReversedZ(FRAGMENT_POSITION.z));
	if (shadowVisibility > 0.0f)
	{
		color += shadowVisibility * CalculateLighting(albedo, roughness, metallic, viewSpaceNormal, viewSpaceIncident, reflectionColor, PassData.ViewSpaceSunlightDirection, PassData.SunlightColor);
	}

	// Perform clustered shading
	@insertpiece(PerformClusteredShading)

	// Emissive term
	@property(UseEmissiveMap)
		color += SAMPLE_2D(EmissiveMap, SamplerLinear, MAIN_INPUT(TexCoordVS)).rgb * material.EmissiveIntensity;
	@end

	// Done
	MAIN_OUTPUT_COLOR(0) = float4(color, material.Opacity);
MAIN_END
